# Maintainer: ledati16 <ledati16@users.noreply.github.com>
pkgname=pwsw-git
pkgver=r0
pkgrel=1
pkgdesc="PipeWire Switcher - Automatically switch audio sinks based on active windows"
arch=('x86_64')
url="https://github.com/ledati16/pwsw"
license=('MIT')
depends=('gcc-libs' 'glibc' 'pipewire')
makedepends=('git' 'cargo')
optdepends=('libnotify: desktop notifications')
provides=('pwsw')
conflicts=('pwsw')
source=("${pkgname%-git}::git+${url}.git")
sha256sums=('SKIP')

pkgver() {
    cd "${pkgname%-git}"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd "${pkgname%-git}"
    export RUSTUP_TOOLCHAIN=stable
    cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
    cd "${pkgname%-git}"
    export RUSTUP_TOOLCHAIN=stable
    export CARGO_TARGET_DIR=target
    cargo build --frozen --release
}

check() {
    cd "${pkgname%-git}"
    export RUSTUP_TOOLCHAIN=stable
    export CARGO_TARGET_DIR=target
    cargo test --frozen
}

package() {
    cd "${pkgname%-git}"
    install -Dm755 target/release/pwsw "$pkgdir/usr/bin/pwsw"
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
    install -Dm644 contrib/pwsw.desktop "$pkgdir/usr/share/applications/pwsw.desktop"
    install -Dm644 contrib/pwsw.service "$pkgdir/usr/lib/systemd/user/pwsw.service"
    for size in 16 24 32 48 64 128 256 512; do
        install -Dm644 "contrib/icons/pwsw-${size}.png" "$pkgdir/usr/share/icons/hicolor/${size}x${size}/apps/pwsw.png"
    done

    # Install man pages (generated during build)
    local _mandir
    _mandir=$(find target/release/build -type d -name man -path "*/pwsw-*/out/man" 2>/dev/null | head -1)
    if [[ -d "$_mandir" ]]; then
        install -Dm644 "$_mandir/pwsw.1" "$pkgdir/usr/share/man/man1/pwsw.1"
        install -Dm644 "$_mandir/pwsw.5" "$pkgdir/usr/share/man/man5/pwsw.5"
    fi
}
